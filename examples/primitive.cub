module primitive where

Id   : (A : U) (a b : A) -> U
Id = PN

refl : (A : U) (a : A) -> Id A a a
refl = PN

funExt : (A : U) (B : (a : A) -> U) (f g : (a : A) -> B a)
         (p : ((x : A) -> (Id (B x) (f x) (g x)))) -> Id ((y : A) -> B y) f g
funExt = PN

inh : U -> U
inh = PN

inc : (A : U) -> A -> inh A
inc = PN

prop : U -> U
prop A = (a b : A) -> Id A a b

squash : (A : U) -> prop (inh A)
squash = PN

inhrec : (A : U) (B : U) (p : prop B) (f : A -> B) (a : inh A) -> B
inhrec = PN

Sigma : (A : U) (B : A -> U) -> U
Sigma A B = (x : A) * B x
-- data Sigma A B = pair (x : A) (y : B x)

fst : (A : U) (B : A -> U) -> Sigma A B -> A
fst A B z = z.1

snd : (A : U) (B : A -> U) (z : Sigma A B) -> B (z.1)
snd A B z = z.2

fiber : (A B : U) (f : A -> B) (y : B) -> U
fiber A B f y = Sigma A (\x -> Id B (f x) y)

id : (A : U) -> A -> A
id A a = a

pathTo : (A:U) -> A -> U
pathTo A = fiber A A (id A)

sId : (A : U) (a : A) -> pathTo A a
sId A a = (a, refl A a)

singl : (A:U) -> A -> U
singl A a = Sigma A (Id A a)

contrSingl :  (A : U) (a b:A) (p:Id A a b) -> Id (singl A a) (a, refl A a) (b, p)
contrSingl = PN

equivEq : (A B : U) (f : A -> B) (s : (y : B) -> fiber A B f y)
            (t : (y : B) -> (v : fiber A B f y) -> Id (fiber A B f y) (s y) v) ->
            Id U A B
equivEq = PN

transport : (A B : U) -> Id U A B -> A -> B
transport = PN

transpInv : (A B : U) -> Id U A B -> B -> A
transpInv = PN

transportRef : (A : U) (a : A) -> Id A a (transport A A (refl U A) a)
transportRef = PN

equivEqRef : (A : U) -> (s : (y : A) -> pathTo A y) -> 
             (t : (y : A) -> (v : pathTo A y) -> Id (pathTo A y) (s y) v) ->
             Id (Id U A A) (refl U A) (equivEq A A (id A) s t)
equivEqRef = PN	       

transpEquivEq : (A B : U) -> (f : A -> B) (s : (y : B) -> fiber A B f y) -> 
                (t : (y : B) -> (v : fiber A B f y) -> Id (fiber A B f y) (s y) v) ->
                (a : A) -> Id B (f a) (transport A B (equivEq A B f s t) a)
transpEquivEq = PN

mapOnPath : (A B : U) (f : A -> B) (a b : A) (p : Id A a b) -> Id B (f a) (f b)
mapOnPath = PN

appOnPath : (A B : U) (f g : A -> B) (a b : A) (q:Id (A->B) f g) (p : Id A a b) -> Id B (f a) (g b)
appOnPath = PN

IdP : (A B :U) -> Id U A B -> A -> B -> U
IdP = PN

IdS : (A:U) (F:A -> U) (a0 a1:A) (p:Id A a0 a1) -> F a0 -> F a1 -> U
IdS A F a0 a1 p = IdP (F a0) (F a1) (mapOnPath A U F a0 a1 p)

mapOnPathD : (A : U) (F : A -> U) (f : (x : A) -> F x) (a0 a1 : A) (p : Id A a0 a1) ->
             IdS A F a0 a1 p  (f a0) (f a1)
mapOnPathD = PN

mapOnPathS : (A:U)(F:A -> U) (C:U) (f: (x:A) -> F x -> C) (a0 a1 : A) (p:Id A a0 a1)
             (b0:F a0) (b1:F a1) (q : IdS A F a0 a1 p b0 b1) -> Id C (f a0 b0) (f a1 b1)
mapOnPathS = PN


-- The circle.
S1 : U
S1 = PN

base : S1
base = PN

loop : Id S1 base base
loop = PN

S1rec : (F : S1 -> U) (b : F base) (l : IdS S1 F base base loop b b) (x : S1) -> F x
S1rec = PN


I : U
I = PN

I0 : I
I0 = PN

I1 : I
I1 = PN

line : Id I I0 I1
line = PN

intrec : (F : I -> U) (s : F I0) (e : F I1)
         (l : IdS I F I0 I1 line s e) (x : I) -> F x
intrec = PN
